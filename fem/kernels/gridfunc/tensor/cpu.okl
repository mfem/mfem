// Copyright (c) 2010, Lawrence Livermore National Security, LLC. Produced at
// the Lawrence Livermore National Laboratory. LLNL-CODE-443211. All Rights
// reserved. See file COPYRIGHT for details.
//
// This file is part of the MFEM library. For more information and source code
// availability see http://mfem.org.
//
// MFEM is free software; you can redistribute it and/or modify it under the
// terms of the GNU Lesser General Public License (as published by the Free
// Software Foundation) version 2.1 dated February 1999.

#include "occa://mfem/fem/defines.okl"

//---[ 1D ]-----------------------------
@kernel void GridFuncToQuad1D(const int numElements,
                              @restrict const DofToQuad_t dofToQuad,
                              @restrict const DLocalMap1D_t l2gMap,
                              @restrict const double * gf,
                              @restrict QVLocal1D_t out) {
  for (int e = 0; e < numElements; ++e; @outer) {
    for (int dummy = 0; dummy < 1; ++dummy; @inner) {
      double r_out[NUM_VDIM][NUM_QUAD_1D];
      for (int v = 0; v < NUM_VDIM; ++v) {
        for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
          r_out[v][qx] = 0;
        }
      }
      for (int dx = 0; dx < NUM_DOFS_1D; ++dx) {
        const int gid = l2gMap(dx, e);
        for (int v = 0; v < NUM_VDIM; ++v) {
          const double r_gf = gf[v + gid*NUM_VDIM];
          for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
            r_out[v][qx] += r_gf * dofToQuad(qx, dx);
          }
        }
      }
      for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
        for (int v = 0; v < NUM_VDIM; ++v) {
          out(v, qx, e) = r_out[v][qx];
        }
      }
    }
  }
}
//======================================


//---[ 2D ]-----------------------------
@kernel void GridFuncToQuad2D(const int numElements,
                              @restrict const DofToQuad_t dofToQuad,
                              @restrict const DLocalMap2D_t l2gMap,
                              @restrict const double * gf,
                              @restrict QVLocal2D_t out) {
  // Iterate over elements
  for (int e = 0; e < numElements; ++e; @outer) {
    for (int dummy = 0; dummy < 1; ++dummy; @inner) {
      double out_xy[NUM_VDIM][NUM_QUAD_1D][NUM_QUAD_1D];
      for (int v = 0; v < NUM_VDIM; ++v) {
        for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
          for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
            out_xy[v][qy][qx] = 0;
          }
        }
      }

      for (int dy = 0; dy < NUM_DOFS_1D; ++dy) {
        double out_x[NUM_VDIM][NUM_QUAD_1D];
        for (int v = 0; v < NUM_VDIM; ++v) {
          for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
            out_x[v][qy] = 0;
          }
        }

        for (int dx = 0; dx < NUM_DOFS_1D; ++dx) {
          const int gid = l2gMap(dx, dy, e);
          for (int v = 0; v < NUM_VDIM; ++v) {
            const double r_gf = gf[v + gid*NUM_VDIM];
            for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
              out_x[v][qy] += r_gf * dofToQuad(qy, dx);
            }
          }
        }

        for (int v = 0; v < NUM_VDIM; ++v) {
          for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
            const double d2q = dofToQuad(qy, dy);
            for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
              out_xy[v][qy][qx] += d2q * out_x[v][qx];
            }
          }
        }
      }

      for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
        for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
          for (int v = 0; v < NUM_VDIM; ++v) {
            out(v, qx, qy, e) = out_xy[v][qy][qx];
          }
        }
      }
    }
  }
}
//======================================


//---[ 3D ]-----------------------------
@kernel void GridFuncToQuad3D(const int numElements,
                              @restrict const DofToQuad_t dofToQuad,
                              @restrict const DLocalMap3D_t l2gMap,
                              @restrict const double * gf,
                              @restrict QVLocal3D_t out) {
  for (int e = 0; e < numElements; ++e; @outer) {
    for (int dummy = 0; dummy < 1; ++dummy; @inner) {
      double out_xyz[NUM_VDIM][NUM_QUAD_1D][NUM_QUAD_1D][NUM_QUAD_1D];
      for (int v = 0; v < NUM_VDIM; ++v) {
        for (int qz = 0; qz < NUM_QUAD_1D; ++qz) {
          for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
            for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
              out_xyz[v][qz][qy][qx] = 0;
            }
          }
        }
      }

      for (int dz = 0; dz < NUM_DOFS_1D; ++dz) {
        double out_xy[NUM_VDIM][NUM_QUAD_1D][NUM_QUAD_1D];
        for (int v = 0; v < NUM_VDIM; ++v) {
          for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
            for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
              out_xy[v][qy][qx] = 0;
            }
          }
        }

        for (int dy = 0; dy < NUM_DOFS_1D; ++dy) {
          double out_x[NUM_VDIM][NUM_QUAD_1D];
          for (int v = 0; v < NUM_VDIM; ++v) {
            for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
              out_x[v][qx] = 0;
            }
          }

          for (int dx = 0; dx < NUM_DOFS_1D; ++dx) {
            const int gid = l2gMap(dx, dy, dz, e);
            for (int v = 0; v < NUM_VDIM; ++v) {
              const double r_gf = gf[v + gid*NUM_VDIM];
              for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
                out_x[v][qx] += r_gf * dofToQuad(qx, dx);
              }
            }
          }

          for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
            const double wy = dofToQuad(qy, dy);
            for (int v = 0; v < NUM_VDIM; ++v) {
              for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
                out_xy[v][qy][qx] += wy * out_x[v][qx];
              }
            }
          }
        }

        for (int qz = 0; qz < NUM_QUAD_1D; ++qz) {
          const double wz = dofToQuad(qz, dz);
          for (int v = 0; v < NUM_VDIM; ++v) {
            for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
              for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
                out_xyz[v][qz][qy][qx] += wz * out_xy[v][qy][qx];
              }
            }
          }
        }
      }

      for (int qz = 0; qz < NUM_QUAD_1D; ++qz) {
        for (int qy = 0; qy < NUM_QUAD_1D; ++qy) {
          for (int qx = 0; qx < NUM_QUAD_1D; ++qx) {
            for (int v = 0; v < NUM_VDIM; ++v) {
              out(v, qx, qy, qz, e) = out_xyz[v][qz][qy][qx];
            }
          }
        }
      }
    }
  }
}
//======================================
